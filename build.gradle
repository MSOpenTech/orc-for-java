apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

sourceCompatibility = 1.6
targetCompatibility = 1.6

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:0.6"
        classpath 'com.android.tools.build:gradle:1.0.0'
    }
}

allprojects {

    repositories {
        jcenter()
    }

    apply plugin: 'idea'
    group = 'com.microsoft.services.orc'
    version = '1.0.0'
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.google.guava:guava:18.0'
    compile 'com.google.code.gson:gson:2.3'
    compile group: 'org.apache.httpcomponents' , name: 'httpclient' , version: '4.3.5'
}

uploadArchives {
    configuration = configurations.archives

    repositories.mavenDeployer {
        pom {
            setGroupId project.mavenGroupId
            setArtifactId project.mavenArtifactId
            setVersion project.mavenVersion
        }
        repository (url: project.mavenRepoUrl) {
            authentication(
                    //TODO: Replace with values from ~/settings.gradle
                    userName: 'bintray',
                    password: 'bintray password'
            )
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
